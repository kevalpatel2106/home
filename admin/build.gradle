apply plugin: 'com.android.application'
apply from: '../misc/config.gradle'
apply from: '../misc/dependencies.gradle'

android {
    signingConfigs {
        playStore {
            if (System.getenv("IS_CI_BUILD").equals("true")) {
                //It is CI build, load it from the environment variables.
                //See circle.yml file.
                storeFile file(System.getenv("KEY_STORE"))
                storePassword System.getenv("KEY_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("ALIAS_PASSWORD")
            } else {
                //If this is the local build, load it from the keystore.properties file
                File signFile = rootProject.file('./misc/keystore.properties')
                if (signFile.exists()) {
                    Properties props = new Properties()
                    props.load(new FileInputStream(signFile))

                    keyAlias props['keystore.alias']
                    keyPassword props['keystore.password']
                    storeFile file(props['keystore.file'])
                    storePassword props['keystore.aliaspassword']
                } else {
                    throw new GradleException("keystore.properties file not found.")
                }
            }
        }
    }

    compileSdkVersion buidlConfig.compile
    buildToolsVersion buidlConfig.buildToolVersion

    compileOptions {
        sourceCompatibility javaCompatibility.sourceCompatibility
        targetCompatibility javaCompatibility.targetCompatibility
    }

    defaultConfig {
        applicationId packageName.admin

        //sdks
        minSdkVersion buidlConfig.min
        targetSdkVersion buidlConfig.target

        //Version config
        versionCode versionConfig.versionCode
        versionName versionConfig.versionName

        //strip other than english resources
        resConfigs "en"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), proguardConfig.proguardPath
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            signingConfig signingConfigs.playStore
            pseudoLocalesEnabled false
            zipAlignEnabled true
        }
        debug {
            debuggable true
            jniDebuggable true
            renderscriptDebuggable false
            signingConfig signingConfigs.playStore
            minifyEnabled false
            zipAlignEnabled true
        }
    }

    packagingOptions packageExcludes

    lintOptions {
        abortOnError false
    }

    aaptOptions {
        cruncherEnabled = false
        useNewCruncher false
    }
}

dependencies {
    compile project(projectModule.network)
    compile project(projectModule.utils)

    //Testing
    testCompile testLib.junit
    androidTestCompile(testLib.rules, excludeGrp)
    androidTestCompile(testLib.esspresso, excludeGrp)
    androidTestCompile(testLib.runner, excludeGrp)
    androidTestCompile(testLib.esspressoIntents, excludeGrp)
    androidTestCompile(testLib.contrib, excludeGrp)

    //Butter knife
    compile butterKnifeLib.butterKnife
    annotationProcessor butterKnifeLib.annotationProcessor

    //Support
    compile supportLib.appCompat
}
